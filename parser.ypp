%{
#include <iostream>
#include <cstdlib>
#include <map>
#include "ast.hpp"
#define YYDEBUG 1

using namespace std;

void yyerror(string s){
  cerr<<s<<endl;
  exit(EXIT_FAILURE);
}

int yylex();

map<string,unsigned> tablica;

%}

%union{
   unsigned u;
   string *s;
   ExprAST *e;
   vector<ExprAST*> *v;
   vector<string> *vs;
}

%token <u> num_token
%token <s> id_token id_liste_token
%token print_token and_token or_token xor_token not_token shl_token shr_token set_token to_token call_token while_token
%type <e> E Naredba Blok
%type <v> NizNaredbi NizE
%type <vs> Args Args1

%left or_token
%left xor_token
%left and_token
%left '<'
%left shr_token
%left shl_token
%left '+'
%right not_token

%%
Program: Program Funkcija
| Funkcija
;
Funkcija: id_token '(' Args ')' Blok {
  FunctionAST f(*$1, *$3, $5);
  delete $1;
  delete $3;

  f.codegen();
 }
;
Args: Args1 {
  $$ = $1;
 }
| {
  $$ = new vector<string>();
 }
;
Args1: Args1 ',' id_token {
  $$ = $1;
  $$->push_back(*$3);
  delete $3;
 }
| id_token {
  $$ = new vector<string>();
  $$->push_back(*$1);
  delete $1;
  }
;
Blok: '{' NizNaredbi '}' {
  $$ = new SeqExprAST(*$2);
  delete $2;
 }
;
NizNaredbi: NizNaredbi ';' Naredba {
  $$ = $1;
  $$->push_back($3);
 }
| Naredba {
  $$ = new vector<ExprAST*>();
  $$->push_back($1);
 }
;
Naredba: print_token E {
  $$ = new PrintExprAST($2);
}
|set_token id_token to_token E {
  $$ = new SetExprAST($4, *$2);
  delete $2;
}
| E {
  $$ = $1;
  }
| Blok {
  $$ = $1;
  }
| while_token '(' E ')' Naredba {
  $$ = new WhileExprAST($3, $5);
  }
;
E: E and_token E {
  $$ = new AndExprAST($1, $3);
}
|E or_token E {
  $$ = new OrExprAST($1, $3);
}
|E xor_token E {
  $$ = new XorExprAST($1, $3);
}
|not_token E {
  $$ = new NotExprAST($2);
}
|E shl_token E {
  $$ = new ShlExprAST($1, $3);
}
|E shr_token E {
  $$ = new ShrExprAST($1, $3);
}
|num_token {
  $$ = new NumberExprAST($1);
}
|'(' E ')' {
  $$ = $2;
}
|id_token {
  $$ = new VariableExprAST(*$1);
  delete $1;
}
| call_token id_token '(' ')' {
  vector<ExprAST*> tmp;
  $$ = new CallExprAST(*$2, tmp);
  delete $2;
}
| call_token id_token '(' NizE ')' {
  $$ = new CallExprAST(*$2, *$4);
  delete $2;
  delete $4;
}
| E '+' E {
  $$ = new AddExprAST($1, $3);
 }
| E '<' E {
  $$ = new LessThenExprAST($1, $3);
 }
;
NizE: NizE ',' E {
  $$ = $1;
  $$->push_back($3);
 }
| E {
  $$ = new vector<ExprAST*>();
  $$->push_back($1);
  }
;
%%

extern Module* TheModule;
extern LLVMContext TheContext;
extern IRBuilder<> Builder;
Value *Str = NULL;
Value *Str1 = NULL;
Function *PrintfFja;

int main(){
  //yydebug = 1;
  InitializeModuleAndPassManager();

  /* printf fja */
  FunctionType *FT1 = FunctionType::get(IntegerType::getInt32Ty(TheContext), PointerType::get(Type::getInt8Ty(TheContext), 0), true);
  PrintfFja = Function::Create(FT1, Function::ExternalLinkage, "printf", TheModule);
  
  yyparse();
  
  TheModule->print(llvm::outs(), nullptr);
  delete TheModule;
  
  return 0;
}
